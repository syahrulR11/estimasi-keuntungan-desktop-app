plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.runtime' version '1.13.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.pdfbox:pdfbox:2.0.30'
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'
}

javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

application {
    mainClass = 'org.App'
}

runtime {
    modules = [
        'java.base', 
        'java.desktop', 
        'javafx.controls', 
        'javafx.fxml',
        'jdk.crypto.ec'
    ]

    jpackage {
        imageName = 'EstimasiKeuntungan'
        installerName = 'EstimasiKeuntungan'
        appVersion = '1.0.0'

        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        installerType = currentOs.windows ? 'exe' : currentOs.macOsX ? 'dmg' : 'deb'
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/org/assets/icon.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Acme Corporation']

        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'estimasikeuntungan','--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'estimasikeuntungan']
        }
    }
}