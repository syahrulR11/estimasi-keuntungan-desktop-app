plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.runtime' version '1.13.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:33.0.0-jre'
}

javafx {
    version = "21.0.7"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'org.App'
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['javafx.controls', 'javafx.fxml']

    additive = true

    jpackage {
        imageName = 'EstimasiKeuntungan'
        installerName = 'EstimasiKeuntungan'
        appVersion = '1.0.0'

        def os = org.gradle.internal.os.OperatingSystem.current()
        if (os.isWindows()) {
            installerType = 'exe'
            installerOptions = [
                '--vendor', 'Syahrul Software',
                '--win-per-user-install',
                '--win-dir-chooser',
                '--win-menu',
                '--win-shortcut',
                '--icon', 'src/main/resources/org/assets/icon.ico'
            ]
        } else if (os.isLinux()) {
            installerType = 'deb'
            installerOptions = [
                '--vendor', 'Syahrul Software',
                '--linux-shortcut',
                '--icon', 'src/main/resources/org/assets/icon.png'
            ]
        }
    }
}